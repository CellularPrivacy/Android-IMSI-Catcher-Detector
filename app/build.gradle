apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion '19.1'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19
    }
    buildTypes {
        debug {
            try {
                buildConfigField 'String', 'OPEN_CELLID_API_KEY', '\"' + open_cellid_api_key + '\"'
            } catch (MissingPropertyException e) {
                buildConfigField 'String', 'OPEN_CELLID_API_KEY', '"NA"'
            }
        }
        release {
            buildConfigField 'String', 'OPEN_CELLID_API_KEY', '"NA"'
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }

    // .. set up build flavors etc here

    // Instead of "app-release.apk" this method will rewrite the name to
    // "MyCoolCompany-MyGreatProduct-v<defaultConfig.versionName>-RELEASE.apk"
    // which is much better suited for archiving and overall handling
    // To restore the default behavior just delete the whole block below
    // E:V:A  Not Working, generates: "AIMSICD-null.apk"
    applicationVariants.all { variant ->

        def apk = variant.outputFile;
        def newName;

        //newName = apk.name.replace(".apk", "-v" + defaultConfig.versionName + "-" + variant.buildType.name.toUpperCase() + ".apk");
        newName = apk.name.replace(".apk", "-" + defaultConfig.versionName + ".apk");

        newName = newName
                .replace("-" + variant.buildType.name, "") // This removes "-release"
                .replace(project.name, "AIMSICD");         // This replace app with "AIMSICD"

        variant.outputFile = new File(apk.parentFile, newName);

        if (variant.zipAlign) {
            variant.outputFile = new File(apk.parentFile, newName.replace("-unaligned", ""));
        }

        logger.info('INFO: Set outputFile to ' + variant.outputFile + " for [" + variant.name + "]");
    }


    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile project(':third_party:RootTools')

    compile 'com.android.support:support-v4:19.1.0'
    compile 'org.osmdroid:osmdroid-android:4.2'
    compile 'eu.lp0.slf4j:slf4j-android:1.7+'
    compile 'org.apache.httpcomponents:httpmime:4.2.1'
    compile 'au.com.bytecode:opencsv:2.4'
}
